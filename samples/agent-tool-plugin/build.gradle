
buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '1.0'
    }
}

apply plugin: 'com.github.rodm.teamcity-agent'
apply plugin: 'com.github.rodm.teamcity-server'

group = 'com.github.rodm.teamcity'
version = '1.0-SNAPSHOT'

ext {
    teamcityVersion = '8.1.5'

    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
    java7Home = project.findProperty('java7.home') ?: '/opt/jdk1.7.0_80'
    java8Home = project.findProperty('java8.home') ?: '/opt/jdk1.8.0_92'
}

repositories {
    mavenCentral()
}

configurations {
    maven
}

dependencies {
    maven group: 'org.apache.maven', name: 'apache-maven', version: '3.5.0', classifier: 'bin', ext: 'zip'
}

agentPlugin {
    baseName = 'maven3_5'
    version = ''
}

serverPlugin {
    baseName = 'maven3_5-tool'
    version = ''
}

teamcity {
    version = teamcityVersion

    agent {
        descriptor {
            toolDeployment {
                executableFiles {
                    include 'bin/mvn'
                }
            }
        }

        files {
            from(zipTree(configurations.maven.singleFile)) {
                includeEmptyDirs = false
                eachFile { file ->
                    file.relativePath = new RelativePath(file.relativePath.endsWithFile, file.relativePath.segments[1..-1] as String[])
                }
            }
        }
    }

    server {
        descriptor {
            name = 'maven3_5-tool'
            displayName = 'Repacked Maven 3.5'
            version = project.version
            vendorName = 'rodm'
            description = 'TeamCity Example Tool Plugin - Repacked Maven 3.5'
            downloadUrl = 'https://github.com/rodm/gradle-teamcity-plugin/'
            email = 'rod.n.mackenzie@gmail.com'
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = 'data'

        teamcity81 {
            version = '8.1.5'
            javaHome = file(java7Home)
        }

        teamcity91 {
            version = '9.1.7'
            javaHome = file(java7Home)
        }

        teamcity10 {
            version = '10.0.5'
            javaHome = file(java8Home)
        }

        teamcity2017 {
            version = '2017.1'
            javaHome = file(java8Home)
        }
    }
}
